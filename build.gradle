group = 'cd.go.contrib.scms.bitkeeper'
version = '0.1-SNAPSHOT'

apply plugin: 'java'

/*
 * Copyright 2016 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// these values that go into plugin.xml
project.ext.pluginDesc = [
        id         : 'cd.go.contrib.scms.bitkeeper',
        version    : project.version,
        goCdVersion: '16.2.1',
        name       : 'BitKeeper plugin for gocd',
        description: 'Plugin that polls a BitKeeper repository for pull requests and triggers a build for each of them',
        vendorName : 'ThoughtWorks Go Team',
        vendorUrl  : 'www.thoughtworks.com'
]

sourceCompatibility = 1.7
targetCompatibility = 1.7

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile group: 'cd.go.plugin', name: 'go-plugin-api', version: project.pluginDesc.goCdVersion
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.4'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
    compile group: 'org.mockito', name: 'mockito-core', version: '2.1.0-RC.1'

    testCompile 'junit:junit:4.12'
}

processResources {
    from("src/main/resource-templates") {
        filesMatching('plugin.xml') {
            expand project.pluginDesc
        }

        filesMatching('plugin.properties') {
            expand project.pluginDesc
        }
    }
}

jar {
    from(configurations.compile) {
        into "lib/"
    }
}
